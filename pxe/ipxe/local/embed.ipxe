#!ipxe

set esc:hex 1b
set bold ${esc:string}[1m
set boldoff ${esc:string}[22m

set white ${esc:string}[37m

#color --rgb 0x76b900 7
#cpair --foreground 7 --background 9 0

:start
echo ${bold}${white}Forge${boldoff} - v${version}
prompt --key p --timeout 4000 Hit the ${bold}p${boldoff} key to open default menu... && goto default || goto carbide

:whoami
ifconf -c dhcp
isset ${43.70} && chain --autofree http://${next-server}:8080/api/v0/pxe/whoami?uuid=${43.70}
prompt --key x Hit the ${bold}x${boldoff} key to continue
goto default

:carbide
ifconf -c dhcp
isset ${43.70} && chain --autofree http://${next-server}:8080/api/v0/pxe/boot?uuid=${43.70}&buildarch=${buildarch}
prompt --key x Hit the ${bold}x${boldoff} key to continue
goto default

:default
menu NGC Forge - ${hostname}
item carbide Boot according to Carbide workflow
item whoami Print information about this machine
item localboot Boot to local drive
#item netconfig Manual network configuration
#item vlan Manual VLAN configuration
item retry Retry boot
item debug iPXE Debug Shell
item reboot Reboot System
choose --default carbide --timeout 5000 target && goto ${target}
prompt --key p --timeout 4000 Hit the ${bold}p${boldoff} key to open default menu...
goto default

:localboot
exit

:retry
goto start

:debug
echo Type "exit" to return to menu
shell
goto default

:reboot
reboot
goto start
